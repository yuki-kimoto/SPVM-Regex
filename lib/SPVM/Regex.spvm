# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Regex {
  version "0.242001";
  
  use Fn;
  use Regex::Replacer;
  use Regex::Re2;
  use Regex::Match;
  use Regex::ReplaceInfo;
  use Hash;
  use List;
  
  has re2 : Regex::Re2;
  has captures : ro string[];
  has match_start : ro int;
  has match_length : ro int;
  has replaced_count : ro int;
  
  static method new : Regex ($pattern : string, $flags : string = undef) {
    
    my $self = new Regex;
    
    my $re2_pattern = $pattern;
    
    if ($flags) {
      $re2_pattern = "(?$flags)$re2_pattern";
    }
    
    $self->compile($re2_pattern);
    
    return $self;
  }
  
  native method DESTROY : void ();
  
  private native method compile : void ($pattern : string);
  
  method match : Regex::Match ($string : string, $offset : int = 0, $length : int = -1) {
    my $regex_match = $self->match_forward($string, \$offset, $length);
    return $regex_match;
  }

  native method match_forward : Regex::Match ($string : string, $offset : int*, $length : int = -1);

  method cap1 : string () { return $self->captures->[1]; }
  method cap2 : string () { return $self->captures->[2]; }
  method cap3 : string () { return $self->captures->[3]; }
  method cap4 : string () { return $self->captures->[4]; }
  method cap5 : string () { return $self->captures->[5]; }
  method cap6 : string () { return $self->captures->[6]; }
  method cap7 : string () { return $self->captures->[7]; }
  method cap8 : string () { return $self->captures->[8]; }
  method cap9 : string () { return $self->captures->[9]; }
  method cap10 : string () { return $self->captures->[10]; }
  method cap11 : string () { return $self->captures->[11]; }
  method cap12 : string () { return $self->captures->[12]; }
  method cap13 : string () { return $self->captures->[13]; }
  method cap14 : string () { return $self->captures->[14]; }
  method cap15 : string () { return $self->captures->[15]; }
  method cap16 : string () { return $self->captures->[16]; }
  method cap17 : string () { return $self->captures->[17]; }
  method cap18 : string () { return $self->captures->[18]; }
  method cap19 : string () { return $self->captures->[19]; }
  method cap20 : string () { return $self->captures->[20]; }

  method replace  : string ($string : string, $replace : object of string|Regex::Replacer, $offset : int = 0, $length : int = -1, $options : object[] = undef) {
    return $self->replace_common($string, $replace, \$offset, $length, $options);
  }
  
  method replace_g  : string ($string : string, $replace : object of string|Regex::Replacer, $offset : int = 0, $length : int = -1, $options : object[] = undef) {
    unless ($options) {
      $options = {};
    }
    $options = Fn->merge_options({global => 1}, $options);
    
    return $self->replace_common($string, $replace, \$offset, $length, $options);
  }
  
  precompile method replace_common : string ($string : string, $replace : object of string|Regex::Replacer, $offset_ref : int*, $length : int = -1, $options : object[] = undef) {
    
    my $optiton_h = Hash->new($options);
    
    my $offset = $$offset_ref;
    my $original_offset = $offset;
    
    my $global = 0;
    if (my $global_obj = $optiton_h->get("global")) {
      $global = (int)$global_obj;
    }
    
    unless ($string) {
      die "The \$string must be defined";
    }
    
    unless ($offset >= 0) {
      die "The \$offset must be greater than or equal to 0";
    }
    
    my $string_length = length $string;
    
    if ($length == -1) {
      $length = $string_length - $offset;
    }
    
    unless ($offset + $length <= $string_length) {
      die "The \$offset + the \$length must be less than or equal to the length of the \$string";
    }
    
    my $result : string;
    my $result_buffer = StringBuffer->new;
    $result_buffer->push($string);
    
    my $regex_match = (Regex::Match)undef;
    my $match_count = 0;
    while (1) {
      my $result_buffer_value = $result_buffer->to_string;
      
      $regex_match = $self->match_forward($result_buffer_value, \$offset, $length);
      
      if ($regex_match) {
        $match_count++;
      }
      else {
        last;
      }
      
      my $replace_string : string;
      if ($replace isa string) {
        $replace_string = (string)$replace;
      }
      elsif ($replace isa Regex::Replacer) {
        my $replacer = (Regex::Replacer)$replace;
        $replace_string = $replacer->($self, $regex_match);
      }
      else {
        die "The \$replace must be a string or a Regex::Replacer object";
      }
      
      my $replace_string_length = length $replace_string;
      
      my $match_start = $regex_match->match_start;
      my $match_length = $regex_match->match_length;
      
      $result_buffer->replace($match_start, $match_length, $replace_string);

      $self->{replaced_count} = $match_count;
      
      unless ($global) {
        last;
      }
      
      my $next_offset = $match_start + $replace_string_length;
      $offset = $next_offset;
      $length = $result_buffer->length - $offset;
    }
    
    my $regex_replace_info = Regex::ReplaceInfo->new({replaced_count => $match_count, match => $regex_match});
    
    my $option_regex_replace_info = (Regex::ReplaceInfo[])$optiton_h->get("info");
    
    if ($option_regex_replace_info) {
      $option_regex_replace_info->[0] = $regex_replace_info;
    }
    
    $result = $result_buffer->to_string;
    
    return $result;
  }

  precompile method split : string[] ($string : string, $limit : int = 0) {
    unless ($string) {
      die "The \$string must be defined";
    }
    
    my $string_length = length $string;
    
    my $parts_list = StringList->new_len(0);
    
    my $offset = 0;
    my $match_count = 0;
    for (my $i = 0; $i < $string_length; $i++) {
      if ($limit > 0 && $match_count >= $limit - 1) {
        last;
      }
      
      my $current_offset = $offset;
      my $regex_match = $self->match_forward($string, \$offset);
      if ($regex_match) {
        $match_count++;

        my $match_start = $regex_match->match_start;
        my $match_length = $regex_match->match_length;
        
        my $part = Fn->substr($string, $current_offset, $match_start - $current_offset);
        $parts_list->push($part);
        my $match_legnth = $self->match_length;
        $offset = $match_start + $match_legnth;
      }
    }
    if ($offset == $string_length) {
      $parts_list->push("");
    }
    else {
      my $part = Fn->substr($string, $offset, $string_length - $offset);
      $parts_list->push($part);
    }

    if ($limit == 0) {
      while ($parts_list->length > 0) {
        if ($parts_list->get($parts_list->length - 1) eq "") {
          $parts_list->pop;
        }
        else {
          last;
        }
      }
    }
    
    my $parts = $parts_list->to_array;
    
    return $parts;
  }
}

